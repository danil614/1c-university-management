#Область ПрограммныйИнтерфейс

Функция ПолучитьСтудентовИзРегистра(УчебнаяГруппа, Факультет, Курс, Регистратор, ЭтотОбъект) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчащиесяСрезПоследних.Студент.Ссылка КАК Студент,
	|	УчащиесяСрезПоследних.ЯвляетсяСтаростой,
	|	УчащиесяСрезПоследних.ОбучениеНаБюджетнойОснове
	|ИЗ
	|	РегистрСведений.Учащиеся.СрезПоследних(&Период, Регистратор <> &Регистратор) КАК УчащиесяСрезПоследних
	|ГДЕ
	|	УчащиесяСрезПоследних.Факультет = &Факультет
	|	И УчащиесяСрезПоследних.УчебнаяГруппа = &УчебнаяГруппа
	|	И УчащиесяСрезПоследних.Курс = &Курс
	|
	|УПОРЯДОЧИТЬ ПО
	|	УчащиесяСрезПоследних.Студент.Наименование";

	Запрос.УстановитьПараметр("УчебнаяГруппа", УчебнаяГруппа);
	Запрос.УстановитьПараметр("Факультет", Факультет);
	Запрос.УстановитьПараметр("Курс", Курс);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Студентов по указанным данным нет!";
		Сообщение.Поле = "Студенты";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		Возврат Неопределено;
	КонецЕсли;

	Возврат РезультатЗапроса.Выгрузить();
КонецФункции

#КонецОбласти