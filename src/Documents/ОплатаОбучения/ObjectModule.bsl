Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	// Обнуляем движения
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Записать();

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВзаиморасчетыОстатки.СуммаОстаток
	|ИЗ
	|	РегистрНакопления.Взаиморасчеты.Остатки(&Период, Студент = &Студент) КАК ВзаиморасчетыОстатки";

	Запрос.УстановитьПараметр("Студент", Студент);
	Запрос.УстановитьПараметр("Период", МоментВремени());

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
		СуммаОстаток = Выборка.СуммаОстаток;
	Иначе
		СуммаОстаток = 0;
	КонецЕсли;

	Если Сумма > СуммаОстаток Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон(
				"Оплата обучения ""%1"" больше суммы остатка ""%2"" для оплаты!", Сумма, СуммаОстаток);
		Сообщение.Поле = "Сумма";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Движения.Взаиморасчеты.Записывать = Истина;
	// Регистр Взаиморасчеты
	Если Сумма > 0 Тогда
		Движение = Движения.Взаиморасчеты.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Студент = Студент;
		Движение.Сумма = Сумма;
	КонецЕсли;
КонецПроцедуры