&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	Объект.Наименование = ОбработкаНаименованияВызовСервера.ФорматироватьНаименование(Объект.Наименование, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКартинку(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборФайла", ЭтотОбъект);
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;

	НачатьПомещениеФайла(ОписаниеОповещения, , ДиалогОткрытияФайла, Истина, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборФайла(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		Фото = Адрес;
		УстановитьКартинку();
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Картинка не была добавлена!";
		Сообщение.Поле = "Фото";
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Отображение картинки при открытии формы справочника
	Фото = ПолучитьНавигационнуюСсылку(Объект.Фото, "Файл");

	// Устанавливаем автомасштаб для картинки
	Элементы.Фото.РазмерКартинки = РазмерКартинки.АвтоРазмер;
КонецПроцедуры

&НаСервере
Процедура УстановитьКартинку()
	// Получаем элемент в Справочнике Файлы
	Если ЗначениеЗаполнено(Объект.Фото) Тогда
		НовыйФайлКартинки = Объект.Фото.ПолучитьОбъект();
	Иначе
		НовыйФайлКартинки = Справочники.Файлы.СоздатьЭлемент();
	КонецЕсли;
	
	// В реквизит СправочникаФайлы Файл помещаем выбранную ранее Картинку
	НовыйФайлКартинки.Файл = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Фото));
	Фото = ПоместитьВоВременноеХранилище(НовыйФайлКартинки.Файл.Получить());
	// Записываем новый элемент справочника Файлы
	НовыйФайлКартинки.Записать();

	// Устанавливаем значение реквизита Картинка Справочника Сотрудники
	Объект.Фото = НовыйФайлКартинки.Ссылка;
КонецПроцедуры